#!/usr/bin/env perl
# Usage:
# $ lazytest [markdown-files...] > test.sh
# $ bash test.sh || echo 'uh oh, something failed'
#
# Looks specifically for ```bash, not ```sh. So you have the option to fence
# with just ```sh if you want syntax highlighting but no test coverage.

use strict;
use warnings;

print <<'EOF';
#!/bin/bash
lazytest_case() {
  local actual="$(eval "$1")"
  local expected="$(cat <&3)"
  if [[ "$actual" = "$expected" ]]; then
    echo "$1" | tr '\n' ' '; echo
    return 0
  else
    echo -e "\033[1;31mFAIL\033[0;0m $*: expected '$expected', got '$actual'"
    return 1
  fi
}
cat <<'LAZYTEST_EOF'
EOF

while (<>) {
  next unless (/^\`\`\`bash/../^\`\`\`$/) && !/^\`\`\`/;

  if (s/^\$\s+//) {
    # We've got a shell command, so read it along with any heredocs we have.
    # Assume all multiline strings are single-quoted, and that an odd number of
    # single-quotes means that we're still inside a string.
    print "LAZYTEST_EOF\n";
    $_ .= <> while 1 & length s/[^']//gr;

    # At this point we've appended our heredoc to the test wrapper, so we
    # should be all set. Look for previous heredocs and scan forwards until
    # they end -- that way we won't break the test case for things like this:
    #
    # $ rev <<'EOF'
    # foo
    # $ bar
    # bif
    # EOF
    # oof
    # rab $
    # fib

    if (/\<\<'?(\w+)'?/) {
      my $hd = $1;
      $_ .= <> until /\n$hd\n$/;
    }

    print "lazytest_case '" . s/'/'\\''/gr . "' 3<<'LAZYTEST_EOF'\n";
  } else {
    print;
  }
}

print "LAZYTEST_EOF\n";
